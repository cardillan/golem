#set target = 8;
#set syntax = strict;
//#set sort-variables;
#set remarks = none;

#set program-name = "Base Builder - Matrices computations";
#set program-version = "v0.4.0";

#set mlog-block-optimization = true;

module Matrices;

def isSolid(x, y)
    var result;
    // Written in pure mlog for speed.
    // Will be rewritten to Mindcode when native support for select gets implemented.
    mlog(in x, in y, out result) {
        ucontrol getBlock x y type building floor
        sensor solid type @solid
        select result notEqual solid false 1 0
    }
    result;
end;

// Creates a 7x7 terrain mask centered around x and y
export def createTerrainMask(unit, x, y)
    ubind(unit);

    var result = 0;
    for var r in -3 .. 3 descending do
        for var c in -3 .. 3 descending do
            result = (result << 1) | isSolid(x + c, y + r);
        end;
    end;

    return result;
end;

const ORE = 1 / 255;
const WALL = 25 / 255;

// Values >= WALL represent walls, regular values should all be less or equal to 16 / 255.
// For 2x2 drills, the highest possible value is 4*25.
// For 3x3 drills, the highest possible value is 9*25 (225).
// For 4x4 drills, the highest possible value would be 16*25, which wouldn't fit in 8 bits.
// For those, the summed column values need to be clamped, resulting in a highest possible value 4*25.
const LIMIT = 20 / 255;

inline def tileValue(x, y, ore)
    var result;
    // Written in mlog for speed.
    // Will be rewritten to Mindcode when native support for select gets implemented.
    mlog(in x, in y, in ore, in ORE, in WALL, out result) {
        ucontrol getBlock x y type building floor
        select value equal floor ore ORE 0
        sensor solid type @solid
        select result notEqual solid false WALL value
    }
    result;
end;

inline def computeRowValues(x, y, ore, size)
    var x1 = tileValue(x - 2, y, ore);
    var x2 = tileValue(x - 1, y, ore);
    var x3 = tileValue(x + 0, y, ore);
    var x4 = tileValue(x + 1, y, ore);
    var x5 = tileValue(x + 2, y, ore);

//    remark($"Spot values for row $y: $, $, $, $, $\n", x1 * 255, x2 * 255, x3 * 255, x4 * 255, x5 * 255);

    if size == 2 then
        // 2x2 drill
        return packcolor(
            x1 + x2,        // drill at x - 1.5
            x2 + x3,        // drill at x - 0.5
            x3 + x4,        // drill at x + 0.5
            x4 + x5         // drill at x + 1.5
        );
    elsif size == 3 then
        // 3x3 drill
        // Using parentheses to help the optimizer
        return packcolor(
            x1 + (x2 + x3),   // drill at x - 1
            (x2 + x3) + x4,   // drill at x
            x3 + x4 + x5,   // drill at x + 1
            0);
    else
        // 4x4 drill - load up additional tiles on the left and right for a 7x7 matrix
        var x0 = tileValue(x - 3, y, ore);
        var x6 = tileValue(x + 3, y, ore);
        // Using parentheses to help the optimizer
        return packcolor(
            min(WALL, x0 + (x1 + x2 + x3)),   // drill at x - 1.5
            min(WALL, (x1 + x2 + x3) + x4),   // drill at x - 0.5
            min(WALL, x2 + (x3 + x4 + x5)),   // drill at x + 0.5
            min(WALL, (x3 + x4 + x5) + x6)    // drill at x + 1.5
        );
    end;
end;

inline void updatePosition(dx, dy, value, ref bestX, ref bestY, ref best)
//    remark($"${value * 255} at $dx, $dy ($bestX, $bestY)");
    if value > best then
        if value < LIMIT then
            bestX = dx;
            bestY = dy;
            best = value;
//            remark(" ***");
        end;
    end;

//    remark("\n");
end;

// Finds the best possible column position over a sum of row values - even sized drill
inline void updateRowPositionsEven(dy, rowSum, ref bestX, ref bestY, ref best)
    var col1, col2, col3, col4;
    unpackcolor(out col1, out col2, out col3, out col4, rowSum);
    updatePosition(-1.5, dy, col1, ref bestX, ref bestY, ref best);
    updatePosition(-0.5, dy, col2, ref bestX, ref bestY, ref best);
    updatePosition(+0.5, dy, col3, ref bestX, ref bestY, ref best);
    updatePosition(+1.5, dy, col4, ref bestX, ref bestY, ref best);
//    remark($"updateRowPositions2x2: $bestX, $bestY: ${best * 255}\n");
end;

// Finds the best possible column position over a sum of row values - odd sized drill
inline void updateRowPositionsOdd(dy, rowSum, ref bestX, ref bestY, ref best)
    var col1, col2, col3;
    unpackcolor(out col1, out col2, out col3, , rowSum);
    updatePosition(-1, dy, col1, ref bestX, ref bestY, ref best);
    updatePosition( 0, dy, col2, ref bestX, ref bestY, ref best);
    updatePosition(+1, dy, col3, ref bestX, ref bestY, ref best);
//    remark($"updateRowPositions3x3: $bestX, $bestY: ${best * 255}\n");
end;

export def findBestPosition(unit, x, y, ore, size, out bestX, out bestY)
    ubind(unit);

    var y1 = computeRowValues(x, y - 2, ore, size);
    var y2 = computeRowValues(x, y - 1, ore, size);
    var y3 = computeRowValues(x, y + 0, ore, size);
    var y4 = computeRowValues(x, y + 1, ore, size);
    var y5 = computeRowValues(x, y + 2, ore, size);

    var best = 0;
    bestX = 0;
    bestY = 0;

    var row1, row2, row3, row4;
    if size == 3 then
            // 3x3 drill
            // Using parentheses to help the optimizer
            row1 = y1 + (y2 + y3);  // drill at y - 1
            row2 = (y2 + y3) + y4;  // drill at y
            row3 = y3 + y4 + y5;    // drill at y + 1

            updateRowPositionsOdd(-1, row1, ref bestX, ref bestY, ref best);
            updateRowPositionsOdd( 0, row2, ref bestX, ref bestY, ref best);
            updateRowPositionsOdd( 1, row3, ref bestX, ref bestY, ref best);
    //        remark($"findBestPosition: $bestX, $bestY: ${best * 255}\n");
    else
        if size == 2 then
            // 2x2 drill
            row1 = y1 + y2;         // drill at y - 1.5
            row2 = y2 + y3;         // drill at y - 0.5
            row3 = y3 + y4;         // drill at y + 0.5
            row4 = y4 + y5;         // drill at y + 1.5
        else
            // 4x4 drill - load up additional rows on top and bottom for a 7x7 matrix
            var y0 = computeRowValues(x, y - 3, ore, size);
            var y6 = computeRowValues(x, y + 3, ore, size);
            // Using parentheses to help the optimizer
            row1 = y0 + (y1 + y2 + y3);     // drill at y - 1.5
            row2 = (y1 + y2 + y3) + y4;     // drill at y - 0.5
            row3 = y2 + (y3 + y4 + y5);     // drill at y + 0.5
            row4 = (y3 + y4 + y5) + y6;     // drill at y + 1.5
        end;

        updateRowPositionsEven(-1.5, row1, ref bestX, ref bestY, ref best);
        updateRowPositionsEven(-0.5, row2, ref bestX, ref bestY, ref best);
        updateRowPositionsEven(+0.5, row3, ref bestX, ref bestY, ref best);
        updateRowPositionsEven(+1.5, row4, ref bestX, ref bestY, ref best);
//        remark($"findBestPosition: $bestX, $bestY: ${best * 255}\n");
    end;

//    debugPause();

    return best * 255;
end;
