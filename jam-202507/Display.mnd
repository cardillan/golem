#set target = 8;
#set syntax = strict;
//#set sort-variables;

module Display;

require "Common.mnd";

linked display1, switch1, message1, bank1,
    oreLocator = processor1,
    timer = processor2;

require "OreLocator.mnd" remote oreLocator;

var
    core = null,
    coreX = 0, coreY = 0,
    page = 0,
    numPages = 3;

noinit remote var
    nextBlock,
    blocksBuilt,
    nextUnit,
    unitReset,
    targetCopper,
    targetLead,
    targetGraphite,
    targetSilicon,
    targetTitanium,
    targetMetaglass,
    targetThorium,
    totalTargets,
    transportTarget,
    transportItem,
    status1,
    status2, item2, block2,
    status3,
    airFactory,
    reconstructor,
    multiReconstructor,
    titaniumDrill,
    titaniumDrillCount,
    thoriumDrill,
    thoriumContainer;

remote var
    recentBuildIndex = -1,
    monos = 0,
    flares = 0,
    polys = 1,
    megas = 0,
    flareTarget = 0,
    monoTarget = 0,
    polyTarget = 0,
    megaTarget = 0;

void unpackCfg2(cfg, out type, out x, out y, out rotation, out size, out index)
    unpackCfg(cfg, out type, out x, out y, , , , , , , , out rotation, out size, out index);
end;

#declare builtin @center, @top, @bottom, @left, @right, @topLeft, @topRight, @bottomLeft, @bottomRight;

inline void drawPrint(x, y, alignment)
    mlogText("draw", "print", in x, in y, in alignment);
end;

inline void drawBold(x, y, alignment, text...)
    drawText(x, y, alignment, text);
    drawText(x + 1, y, alignment, text);
end;

inline void drawText(x, y, alignment)
    drawPrint(x, y, alignment);
end;

inline void drawText(x, y, alignment, text...)
    print(text);
    drawPrint(x, y, alignment);
end;

noinit var materialCount;

void updateDisplay()
    reset();
    clear(0, 0, 32);

    drawHeader();
    drawMaterials();
    drawUnit();
    drawUnits();
    drawflush(display1);
end;

const ROW_HEIGHT = 15;
const BAR_HEIGHT = 10;
const BAR_X = 22;

const ICON_SIZE = 16;
const ICON_X = 11;
const ICON_Y = +5;

const BAR_LENGTH = 122;
const BAR_LENGTH2 = 34;

const HEADER_X = 176 / 4 - 4;
const HEADER1_Y = 176 - 15;
const HEADER2_Y = HEADER1_Y - 56;
const HEADER3_Y = HEADER2_Y - 4 * ROW_HEIGHT - 5;
const HEADER4_Y = HEADER2_Y - 5 * ROW_HEIGHT - 5;

var animation = -15;

inline def lerp(from, to, ratio)
    (1 - ratio) * from + ratio * to;
end;

inline void animate()
    if animation < 0 then
        color(0, 0, 32, 80);
        rect(0, 0, 176, 176);
    else
        reset();
        clear(0, 0, 32);
    end;

    col(%[white]);
    var ratio = max(0, min(animation / 30, 1));
    var x = lerp(HEADER_X, 176 / 2, ratio);
    var y = lerp(HEADER1_Y - 24, 176 / 2, ratio);
    var size = lerp(32, 168, ratio);
    image(x, y, @mega, size, 0);
    drawflush(display1);
    animation++;
end;

inline void drawHeader()
    col(%[gold]);
    drawBold(HEADER_X, HEADER1_Y, @bottom, "Goal");

    col(%[white]);
    var count = nextBlock + nextUnit;
    if count == 2 then
        image(HEADER_X - 15, HEADER1_Y - 22, nextBlock, 25, 0);
        image(HEADER_X + 15, HEADER1_Y - 22, nextUnit, 25, 0);
    elsif count == 1 then
        image(HEADER_X, HEADER1_Y - 24, nextBlock != null ? nextBlock : nextUnit, 32, 0);
    else
        image(HEADER_X, HEADER1_Y - 24, @hydrogen, 32, 360 - (@tick / 2) % 360);
    end;

    var tt = @second;
    var t = floor(timer.read("time") / 120);
    var m = t \ 60;
    var s = t % 60;
    print(m, ":", s < 10 ? "0" : "", s);

    var ratio = round(10 * t / tt);
    print(" (x", ratio \ 10, ".", ratio % 10);

    drawText(170, 172, @topRight, ")");

    if titaniumDrill then
        count = max(titaniumDrillCount, titaniumDrill.@titanium);
        var x = 170;
        var len = floor(log10(count));
        drawText(x, 155, @topRight, "x", count);
        image(x - 20 - 7 * len, 146, @titanium, 12, 0);
    end;

    if thoriumContainer then
        count = thoriumContainer.@thorium;
        var x = 120;
        var len = floor(log10(count));
        drawText(x, 155, @topRight, "x", count);
        image(x - 20 - 7 * len, 146, @thorium, 12, 0);
    end;
end;

noinit var factoryProgress, reconstructorProgress;

inline void drawMaterials()
    noinit var targetFactoryLead, targetFactorySilicon;
    var count, col = 0, y = HEADER2_Y - ROW_HEIGHT;

    if nextBlock == null then
        factoryProgress = max(factoryProgress, airFactory.@progress);
        reconstructorProgress = max(reconstructorProgress, reconstructor.@progress);
        var multiReconstructorProgress = multiReconstructor.@progress;
        noinit var unit1, unit2, unit3;
        noinit var unitCount1, unitCount2;
        noinit var unitTotal1, unitTotal2;

        if nextUnit == @mono then
            targetFactoryLead = 15;
            targetFactorySilicon = 30;
            materialCount = 2;
            unitCount1 = monos;
            unitTotal1 = monoTarget;
            unit1 = nextUnit;
            unit2 = unit3 = null;
        elsif nextUnit == @flare then
            targetFactoryLead = 0;
            targetFactorySilicon = 15;
            materialCount = 1;
            unitCount1 = flares;
            unitTotal1 = flareTarget;
            unit1 = nextUnit;
            unit2 = unit3 = null;
        elsif nextUnit == @poly then
            unitCount1 = floor(factoryProgress);
            unitCount2 = polys;
            unitTotal1 = 1;
            unitTotal2 = polyTarget;
            unit1 = @mono;
            unit2 = nextUnit;
            unit3 = null;
            factoryProgress = max(factoryProgress, reconstructorProgress > 0);
        elsif nextUnit == @mega then
            unitCount1 = floor(factoryProgress);
            unitCount2 = floor(reconstructorProgress);
            unitTotal1 = 1;
            unitTotal2 = 1;
            unit1 = @mono;
            unit2 = @poly;
            unit3 = nextUnit;
            factoryProgress = max(factoryProgress, reconstructorProgress > 0);
            reconstructorProgress = max(reconstructorProgress, multiReconstructorProgress > 0);
        end;

        drawBold(BAR_X, HEADER2_Y, @bottomLeft, "Unit factories");

        if factoryProgress > 0 then
            drawItem(in 0, in HEADER2_Y - ROW_HEIGHT, 1, unit1, factoryProgress, 1.0, unitCount1, "/", unitTotal1);
            if unit2 != null then
                drawItem(in 0, in HEADER2_Y - 2 * ROW_HEIGHT, 1, unit2, reconstructorProgress, 1.0, unitCount2, "/", unitTotal2);
                if unit3 != null then
                    drawItem(in 0, in HEADER2_Y - 3 * ROW_HEIGHT, 1, unit3, multiReconstructorProgress, 1.0, megas, "/", megaTarget);
                end;
            end;
        else
            for var item, target in
                @lead,    targetFactoryLead,
                @silicon, targetFactorySilicon
            do
                if target > 0 then
                    drawItem(in out col, in out y, 1, item, airFactory.sensor(item), target, target);
                end;
            end;
        end;
    elsif nextBlock == @titanium or nextBlock == @ore-titanium then
        drawBold(BAR_X, HEADER2_Y, @bottomLeft, oreLocator.mapAction);
        drawItem(in 0, in HEADER2_Y - ROW_HEIGHT, 1, @titanium, oreLocator.mapProgress, oreLocator.mapTotal, "x", oreLocator.mapSpots);
    else
        count = materialCount = totalTargets;
        drawBold(BAR_X, HEADER2_Y, @bottomLeft, "Materials");
        var columns = 1;

        if count then
            for var item, target in
                @copper,    targetCopper,
                @lead,      targetLead,
                @graphite,  targetGraphite,
                @silicon,   targetSilicon,
                @titanium,  targetTitanium,
                @metaglass, targetMetaglass,
                @thorium,   targetThorium
            do
                if target > 0 then
                    drawItem(in out col, in out y, columns, item, core.sensor(item), target, target);
                    if --count <= 0 then break; end;
                end;
            end;
        end;
    end;
end;

inline void drawUnit()
    col(%[white]);
    var y = materialCount < 4 ? HEADER3_Y : HEADER4_Y;
    drawBold(BAR_X, y, @bottomLeft, "Builder");

    if transportItem == null then
        drawUnit(@poly, 0, in y - ROW_HEIGHT);
    else
        var unitItem = @unit.@firstItem;
        drawUnit(transportItem, @unit.@totalItems * (unitItem == transportItem), in y - ROW_HEIGHT);
    end;
end;

inline void drawUnit(item, items, in out y)
     drawItem(in 0, in out y, 1, item, items, transportTarget, transportTarget);
end;

inline void drawItem(in out col, in out y, columns, item, current, target, text...)
    var barLength = BAR_LENGTH + 176 / 2 - (columns * 176 / 2);
    var textEnd = 170 - BAR_LENGTH + barLength;
    var length = min(barLength, barLength * current / target);

    var iconX = ICON_X + col * 176 / 2;
    var barX = BAR_X + col * 176 / 2;
    var textX = textEnd + col * 176 / 2;

    col(%[white]);
    image(iconX, y + ICON_Y, item, 16, 0);

    color(0,128,0,255);
    rect(barX, y, length, BAR_HEIGHT);

    color(0,255,0,255);
    lineRect(barX, y, barLength, BAR_HEIGHT);
    drawBold(textX, y + ICON_Y + 2, @right, text);

    if ++col >= columns then
        col = 0;
        y -= ROW_HEIGHT;
    end;
end;

inline void drawUnits()
    var x = 170;
    col(%[white]);
    drawUnitCount(@poly, polys, ref x);
    drawUnitCount(@mono, monos, ref x);
    drawUnitCount(@flare, flares, ref x);
end;

inline void drawUnitCount(type, count, ref x)
    if count > 0 then
        image(x - 20, HEADER1_Y - 34, type, 12, 0);
        drawText(x, HEADER1_Y - 32, @right, "x", count);
        x -= 30;
    end;
end;

void updateMessage()
    println($"[gold]Building $nextBlock");
    println("==================================[]");
    println(status1);
    println($"$status2$item2 ($block2)");
    println(status3);
    printflush(message1);
end;

inline void displayRecordAt(ref x, ref y, image, text)
    image(x, y, image, 14, 0);
    drawText(x + 56 - 12, y + 2, @right, text);

    y -= 15;
    if y < 10 then
        x += 58;
        y = 176 - 4 - 8;
    end;
end;


inline void displayRecord(ref x, ref y, record)
    var id = record % 1000;
    var icon = id > @blockCount ? lookup(:unit, id - @blockCount) : lookup(:block, id);
    var t = floor(record / 1000 / 120);
    var m = t \ 60;
    var s = t % 60;
    print(m, ":", s < 10 ? "0" : "");
    displayRecordAt(ref x, ref y, icon, s);
end;

inline void displayRecord2(ref x, ref y, record)
    displayRecordAt(ref x, ref y,  @core-shard, "0:00");
end;

void displayLog(page)
    var x = 3 + 8;
    var y = 176 - 5 - 8;

    var logPosition = bank1[511];
    var records = (511 - logPosition);
    var pages = max(1, (records - 1) \ 32 + 1);
    numPages = pages + 2;
    var start = 511 - 32 * page;
    var stop = max(logPosition, start - 32);

    col(%[lightgray]);
    print(page + 1, "/");
    drawText(172, 4, @bottomRight, pages);

    col(%[white]);
    for var position in stop ... start descending do
        var record = bank1[position];
        displayRecord(ref x, ref y, record);
        drawflush(display1);
        if switch1.enabled then break; end;
    end;
end;

//external bank1[0 ... 4] var minX, minY, maxX, maxY;

var mapw = @mapw, maph = @maph;

void displayOreMap()
    reset();
    clear(0, 0, 32);

    col(%[gold]);
    drawText(4, 172, @topLeft, "Map");

    col(%[white]);
    scale(176 / mapw, 176 / maph);
    var imgSize = ceil(mapw / 176);
    //stroke(max(width, height));

    image(coreX, coreY, @core-shard, 7 * imgSize, 0);

    var index = -1, count = oreLocator.locations;
    for var i in 0 ... count do
        var id = bank1[++index];
        var img = id > 0 ? lookup(:item, id) : lookup(:block, -id);
        var size = 4 + sqrt(bank1[++index]);
        var x = bank1[++index];
        var y = bank1[++index];
        image(x, y, img, size * imgSize, 0);
    end;

    image(titaniumDrill.@x, titaniumDrill.@y, @pneumatic-drill, 4 * imgSize, 0);
    image(thoriumDrill.@x, thoriumDrill.@y, @laser-drill, 5 * imgSize, 0);

    reset();
    col(%[white]);
    drawText(175, 172, @topRight, index);

    drawflush(display1);
end;

void drawBlock(cfg)
    var type, x, y, rotation, size, index;
    unpackCfg2(cfg, out type, out x, out y, out rotation, out size, out index);
    if x | y then
        if blocksBuilt & (1 << index) then
            col(%[white]);
        else
            color(128, 255, 128, 128);
        end;
        image(2 * x, 2 * y, type, 2 * size, 90 * rotation);
    end;
end;

void displayBaseMap()
    reset();
    clear(0, 0, 32);
    col(%[gold]);
    drawText(172, 172, @topRight, "Base map");

    col(%[white]);
    translate(176 / 2, 176 / 2);
    scale(176 / 15 / 2, 176 / 15 / 2);

    col(%[white]);
    image(0, 0, @core-shard, 6, 0);

    //color(0, 255, 0, 128);

    for var cfg in BLOCKS do
        drawBlock(cfg);
        var i = recentBuildIndex;
        if i >= 0 then
            recentBuildIndex = -1;
            drawBlock(BLOCKS[i]);
        end;

        if switch1.enabled then
            drawflush(display1);
            break;
        end;
        if pageSwitched then
            drawflush(display1);
        end;
    end;
    drawflush(display1);
end;

var pageSwitched = false;

void main()
    do
        ubind(@poly);
        core = ulocate(:building, :core, false);
    while core == null;
    coreX = core.@x;
    coreY = core.@y;

    printflush(null);
    drawflush(null);

    while true do
        if unitReset then
            factoryProgress = reconstructorProgress = 0;
            unitReset = false;
        end;

        if page == -1 then
            displayOreMap();
        elsif page == 0 then
            if megas > 0 then
                animate();
            else
                updateDisplay();
            end;
        elsif page == 1 then
            //displayOreMap();
            displayBaseMap();
        else
            displayLog(page - 2);
        end;

        if megas > 0 then
            println("[gold]All done.[]\nNothing more to see here.\nGo home.");
            printflush(message1);
        else
            updateMessage();
        end;

        pageSwitched = switch1.enabled;
        if pageSwitched then
            switch1.enabled = false;
            page = (page + 1) % numPages;
            drawflush(null);
            reset();
            clear(0, 0, 32);
        end;
    end;
end;

void backgroundProcess()
    main();
end;
