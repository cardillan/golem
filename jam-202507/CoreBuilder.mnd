#set target = 8;
#set syntax = strict;
//#set sort-variables;

// Linked variables
linked message1, bank1, processor1, processor2, processor3, processor4, processor5,
    timer = processor6,
    flareController = processor7,
    monoController = processor8;

noinit var controller;

var display = processor1,
    polyController = processor3,
    blockBuilder = processor5,
    oreLocator = processor4;

require units;
require "Common.mnd";
require "Actions.mnd";
require "Display.mnd" remote display;
require "OreLocator.mnd" remote oreLocator;
require "CoreController.mnd" remote controller;
require "MonoController.mnd" remote monoController;
require "PolyController.mnd" remote polyController;
require "BlockBuilder.mnd" remote blockBuilder;

// At this point, we're only using a poly
const capacity = 30;

// Built blocks
var
    press = null,
    generator = null,
    battery = null,
    smelter = null,
    titaniumDrill = null,
    unloader1 = null,
    kiln = null,
    thoriumDrill = null,
    thoriumContainer = null,
    thoriumGenerator = null;

noinit var
    core, coreX, coreY,
    builder,

    // Targets to be met for the next build
    targetCopper,
    targetLead,
    targetGraphite,
    targetSilicon,
    targetTitanium,
    targetMetaglass,
    targetThorium,
    powerTarget;

var
    leadSurplus = 0,
    graphiteSurplus = 20,
    siliconSurplus = 20,
    metaglassSurplus = 20;

external bank1[511] logPosition = 511;

void unpackCfg(cfg, out type, out x, out y, out cu, out pb, out gr, out si, out ti, out mg, out th, out rotation, out ind)
    var index = 0;
    type = lookup(:block, char(cfg, index++) - 35);
    x = char(cfg, index++) - 35 - 7;
    y = char(cfg, index++) - 35 - 7;
    cu = char(cfg, index++) - 35;
    pb = char(cfg, index++) - 35;
    gr = char(cfg, index++) - 35;
    si = char(cfg, index++) - 35;
    ti = char(cfg, index++) - 35;
    mg = char(cfg, index++) - 35;
    th = char(cfg, index++) - 35;
    rotation = char(cfg, index++) - 35;
    var size = char(cfg, index++) - 35;
    ind = char(cfg, index++) - 35;
    //println($"Type: $type, x: $x, y: $y, $cu, $pb, $gr, $si, $ti, $mg, $rotation");
end;

// SUPPLY OPERATIONS

inline void supplyBase0()
    var supplied = false;

    if unloader1 == null then
        if core.@graphite < targetGraphite then
            if press.@coal < 5 then
                status1("Goal 1: graphite requirement");
                supplyBlock(press, @coal);
                supplied = true;
            end;
        end;

        if core.@silicon < targetSilicon then
            if smelter.@coal < 5 then
                status1("Goal 2: coal for silicon requirement");
                supplyBlock(smelter, @coal);
                supplied = true;
            elsif smelter.@sand < 5 then
                status1("Goal 3: sand for silicon requirement");
                supplyBlock(smelter, @sand);
                supplied = true;
            end;
        end;

        if battery.@totalPower < powerTarget then
            if generator.@coal < 2 then
                status1("Goal 4: supplying generator");
                supplyBlock(generator, @coal);
                supplied = true;
            end;
       end;
    end;

    var titaniumTrigger = core.@titanium < targetTitanium ? min(7, targetTitanium - core.@titanium) : 100;
    if titaniumDrill.@titanium < titaniumTrigger then
        if !supplied then
            if battery.@totalPower < unloader1 * powerTarget then
                status1("Goal 5: supplying generator");
                mineOre(@coal);
            elsif core.@silicon < targetSilicon then
                status1("Goal 6: silicon requirement");
                var coal = core.@coal;
                var sand = core.@sand;
                var ore = coal < sand ? @coal : @sand;
                mineOre(ore);
            elsif core.@copper < targetCopper then
                status1("Goal 7: copper requirement");
                mineOre(@copper);
            elsif core.@lead < targetLead then
                status1("Goal 8: lead requirement");
                mineOre(@lead);
            elsif core.@metaglass < targetMetaglass then
                status1("Goal 9: metaglass requirement");
                mineOre(@sand);
            else
                status1("Goal 10: supplying base");
                var coal = core.@coal;
                var sand = core.@sand;
                var ore = coal <= sand ? @coal : @sand;
                mineOre(ore);
            end;
        end;
    else
        status1("Goal 11: Fetching titanium");
        fetchBlock(titaniumDrill, @titanium);
    end;
end;

inline void supplyBase1()
    status1("Phase 1: supplying base");
    mineOre(@coal);
end;

inline void supplyBase2()
    status1("Phase 2: servicing thorium drill");

    if core.@coal < 50 then
        supplyBlock(thoriumGenerator, @coal);
    else
        supplyBlockFromCore(thoriumGenerator, @coal);
    end;
    fetchBlock(thoriumContainer, @thorium);
end;

void supplyBase()
    monoController.sandTarget = max(0, targetSilicon - core.@silicon);

    if thoriumContainer then
        supplyBase2();
    elsif controller.monos > 3 then
        supplyBase1();
    else
        supplyBase0();
    end;
end;

inline def targetMet()
    // This is horrible, but Mindcode doesn't do short-circuit evaluation yet.
    if core.@copper < targetCopper then return false; end;
    if core.@lead < targetLead then return false; end;
    if core.@graphite < targetGraphite then return false; end;
    if core.@silicon < targetSilicon then return false; end;
    if core.@titanium < targetTitanium then return false; end;
    if core.@metaglass < targetMetaglass then return false; end;
    if core.@thorium < targetThorium then return false; end;
    return true;
end;

void meetTarget(block, copper, lead, graphite, silicon, titanium, metaglass, thorium)
    // Update display
    var count = 0;
    for var t in copper, lead, graphite, silicon, titanium, metaglass, thorium do count += sign(t); end;
    display.totalTargets = count;
    display.nextBlock = block;
    display.targetCopper = copper;
    display.targetLead = lead;
    display.targetGraphite = graphite;
    display.targetSilicon = silicon;
    display.targetTitanium = titanium;
    display.targetMetaglass = metaglass;
    display.targetThorium = thorium;

    targetCopper = copper;
    targetLead = lead + leadSurplus + 1;
    targetGraphite = graphite;
    targetSilicon = silicon;
    targetTitanium = titanium;
    targetMetaglass = metaglass;
    targetThorium = thorium;

    controller.pressTarget = graphite + graphiteSurplus;
    controller.smelterTarget = silicon + siliconSurplus;
    controller.kilnTarget = metaglass + metaglassSurplus;
    controller.leadReserve = lead + leadSurplus;
    controller.siliconReserve = silicon + sign(silicon) * 20;
    controller.graphiteReserve = graphite + sign(graphite) * 20;

    monoController.leadTarget = targetLead;
    monoController.copperTarget = targetCopper;

    while !targetMet() do
        supplyBase();
    end;
end;

// BUILDING OPERATIONS

def buildBlock(block, x, y, copper, lead, graphite, silicon, titanium, metaglass, thorium, rotation, config, index)
    meetTarget(block, copper, lead, graphite, silicon, titanium, metaglass, thorium);
    return blockBuilder.buildBlockAt(block, x, y, rotation, config, index);
end;

def buildRemoteBlock(cfg, x, y)
    if x + y > 0 then
        var type, cu, pb, gr, si, ti, mg, th, rotation, index;
        unpackCfg(cfg, out type, , , out cu, out pb, out gr, out si, out ti, out mg, out th, out rotation, out index);
        return buildBlock(type, x, y, cu, pb, gr, si, ti, mg, th, rotation, null, index);
    end;
    return false;
end;

// Builds a block
def buildBlockFromCfg(cfg, config)
    var type, x, y, cu, pb, gr, si, ti, mg, th, rotation, index;
    unpackCfg(cfg, out type, out x, out y, out cu, out pb, out gr, out si, out ti, out mg, out th, out rotation, out index);
    return buildBlock(type, coreX + x, coreY + y, cu, pb, gr, si, ti, mg, th, rotation, config, index);
end;

def buildBlockFromCfg(cfg)
    buildBlockFromCfg(cfg, null);
end;

void meetTarget(cfg)
    var type, cu, pb, gr, si, ti, mg, th;
    unpackCfg(cfg, out type, , , out cu, out pb, out gr, out si, out ti, out mg, out th);
    meetTarget(type, cu, pb, gr, si, ti, mg, th);
end;

def buildFirstTitaniumDrill(cfg)
    meetTarget(cfg);

    display.nextBlock = @titanium;
    var dx, dy;
    var result = oreLocator.findDrillLocation(coreX, coreY, @unit, @titanium, @ore-titanium, 2, 4, out dx, out dy);
    if !result then error("Cannot locate spot for titanium drill"); end;
    display.nextBlock = @pneumatic-drill;

    return blockBuilder.buildFirstTitaniumDrillAt(dx, dy);
end;

void blocksBuildTitaniumDrills()
    // Make sure to bind and flag the flare in a single tick
    wait(1e-9);
    ubind(@flare);

    var current = oreLocator.locations;
    async(oreLocator.findDrillLocation(coreX, coreY, @unit, @titanium, @ore-titanium, 4, 1e9));

    // Bind the main builder back
    ubind(builder);

    blockBuilder.buildTitaniumDrills(controller, current);
    async(oreLocator.stopLocation());
end;


void buildThoriumDrill()
    // We're using the main builder for this, and calling synchronously
    oreLocator.layoutThoriumDrill(coreX, coreY, @unit, 7);

    meetTarget(LASER_DRILL);

    var result = await(oreLocator.layoutThoriumDrill);
    if !result then error("Cannot locate spot for thorium drill"); end;
    thoriumDrill = blockBuilder.buildBlockAt(@laser-drill, oreLocator.drillX, oreLocator.drillY, null, 0, -1);

    buildRemoteBlock(WATER_EXT, oreLocator.water1X, oreLocator.water1Y);
    buildRemoteBlock(WATER_EXT, oreLocator.water2X, oreLocator.water2Y);
    thoriumGenerator = buildRemoteBlock(STEAM_GEN, oreLocator.generatorX, oreLocator.generatorY);
    buildRemoteBlock(POWER_NODE, oreLocator.node1X, oreLocator.node1Y);
    buildRemoteBlock(POWER_NODE, oreLocator.node2X, oreLocator.node2Y);
    buildRemoteBlock(SOLAR_PANEL, oreLocator.panelX, oreLocator.panelY);
    buildRemoteBlock(BATTERY1, oreLocator.batteryX, oreLocator.batteryY);
    thoriumContainer = buildRemoteBlock(CONTAINER, oreLocator.containerX, oreLocator.containerY);
    if thoriumContainer == null then thoriumContainer = thoriumDrill; end;
    display.thoriumDrill = thoriumDrill;
    display.thoriumContainer = thoriumContainer;

    polyController.setThoriumContainer(thoriumContainer);
end;

void buildMultiplicativeReconstructor()
//    meetTarget(MULTIPLICATIVE_REC);
//    async(polyController.pause());

    var multiReconstructor = buildBlockFromCfg(MULTIPLICATIVE_REC);
    controller.multiReconstructor = multiReconstructor;
    display.multiReconstructor = multiReconstructor;

//    do
//        ubind(@poly);
//        moveTo(coreX, coreY);
//        itemTake(core, @silicon, capacity);
//        itemDrop(multiReconstructor, capacity);
//    while multiReconstructor.@silicon < 130;
//
//    async(polyController.resume());
end;

// MAIN

void buildUnits()
    meetTarget(0, 0, 0, 0, 0, 0, 0, 0);
    display.totalTargets = 0;
    display.nextBlock = null;
    controller.leadReserve = 0;
    controller.siliconReserve = 0;
    controller.graphiteReserve = 0;
end;

begin
    // INITIALIZATION

    do
        builder = ubind(@poly);
        core = ulocate(:building, :core, false, out coreX, out coreY);
    while core == null;

    // PHASE 0: BASIC BLOCKS

    press = buildBlockFromCfg(PRESS);
    battery = buildBlockFromCfg(BATTERY1);
    buildBlockFromCfg(BATTERY2);
    generator = buildBlockFromCfg(GENERATOR);

    powerTarget = 400;

    smelter = buildBlockFromCfg(SMELTER);
    buildBlockFromCfg(INVERTED_SORT1);
    titaniumDrill = buildFirstTitaniumDrill(PNEUMATIC_DRILL);
    display.titaniumDrill = titaniumDrill;
    unloader1 = buildBlockFromCfg(UNLOADER1);
    buildBlockFromCfg(INVERTED_SORT2);
    buildBlockFromCfg(OVERFLOW_GATE1);
    graphiteSurplus = 100;

    // Build and initialize base controller

    controller = buildBlockFromCfg(PROCESSOR, processor2);
    controller.timer = timer;
    controller.display = display;
    controller.core = core;
    controller.bank = bank1;
    controller.battery = battery;
    controller.generator = generator;
    controller.smelter = smelter;
    controller.press = press;
    controller.unloader1 = unloader1;

    buildBlockFromCfg(POWER_NODE);
    controller.unloader2 = buildBlockFromCfg(UNLOADER2);

    var factory = buildBlockFromCfg(AIR_FACTORY);
    controller.factory = factory;
    display.airFactory = factory;

    // PHASE 1: BUILDING UNITS

    setFlareTarget(1);
    setMonoTarget(3);
    controller.pressTarget = 85;
    controller.smelterTarget = 150;
    controller.kilnTarget = 30;
    siliconSurplus = leadSurplus = 20;

    buildUnits();
    do
        supplyBase();

        // We're low on power: the air units might not get built in time
        // Go on building additional power reserves yourself.
        if battery.@totalPower < 300 then break; end;
    while controller.monos < 3;

    // Build stock
    siliconSurplus = 65;

    // Kiln - needed for metaglass
    buildBlockFromCfg(UNLOADER3);
    buildBlockFromCfg(INVERTED_SORT3);
    kiln = buildBlockFromCfg(KILN);
    controller.kiln = kiln;

    // Steam generator
    buildBlockFromCfg(WATER_EXT);
    buildBlockFromCfg(OVERFLOW_GATE2);
    buildBlockFromCfg(OVERFLOW_GATE3);
    controller.generator2 = buildBlockFromCfg(STEAM_GEN);

    controller.multipress = buildBlockFromCfg(MULTIPRESS);
    //buildBlockFromCfg(WATER_EXT2);

    //buildBlockFromCfg(BATTERY_LARGE);
    graphiteSurplus = 150;

    // POLYS
    controller.payloadRouter = buildBlockFromCfg(PAYLOAD_ROUTER);
    controller.unloader4 = buildBlockFromCfg(UNLOADER4);
    buildBlockFromCfg(UNDERFLOW_GATE);
    var reconstructor = buildBlockFromCfg(ADDITIVE_REC);
    controller.reconstructor = reconstructor;
    display.reconstructor = reconstructor;
    buildBlockFromCfg(BATTERY_LARGE);

    buildUnits();

    // Even more stock
    siliconSurplus = 1000;
    metaglassSurplus = 200;

    setPolyTarget(6);
    controller.smelter2 = buildBlockFromCfg(SMELTER2);
    buildBlockFromCfg(INVERTED_SORT4);
    buildBlockFromCfg(INVERTED_SORT5);
    buildBlockFromCfg(UNLOADER5, @sand);
    buildUnits();

    while controller.polys < 3 do
        supplyBase();
    end;

    setFlareTarget(3);
    while controller.flares < 2 do
        supplyBase();
    end;

    graphiteSurplus = 100;
    setMonoTarget(5);

    // Build more titanium drills now
    // The base can already be fully served by monos and polys,
    // the main builder no longer needs to be occupied by supplying base
    // Note: display three concurrent goals
    display.nextBlock = @ore-titanium;
    blocksBuildTitaniumDrills();
    display.nextBlock = null;

    buildThoriumDrill();
    display.nextBlock = null;

    setMegaTarget(1);
    while controller.polys < 5 do
        supplyBase();
    end;

    controller.unloader6 = buildBlockFromCfg(UNLOADER6, @silicon);
    buildBlockFromCfg(UNDERFLOW_GATE2);
    buildMultiplicativeReconstructor();

    metaglassSurplus = 50;
    buildUnits();

    while controller.megas < 1 do
        supplyBase();
    end;

    stopAll();
end;

inline void setFlareTarget(target)
    controller.flareTarget = target;
    display.flareTarget = target;
end;

inline void setMonoTarget(target)
    controller.monoTarget = target;
    display.monoTarget = target;
end;

inline void setPolyTarget(target)
    controller.polyTarget = target;
    display.polyTarget = target;
end;

inline void setMegaTarget(target)
    controller.megaTarget = target;
    display.megaTarget = target;
end;

void stopAll()
    flareController.enabled = false;
    monoController.enabled = false;
    processor3.enabled = false;
    processor4.enabled = false;
    processor5.enabled = false;

    flagUnits(@mega);
    flagUnits(@poly);
    flagUnits(@mono);
    flagUnits(@flare);

    while true do
        lineUpUnits(@thisy -  6, @mega);
        lineUpUnits(@thisy - 10, @poly);
        lineUpUnits(@thisy - 14, @mono);
        lineUpUnits(@thisy - 18, @flare);
    end;
end;

void flagUnits(unit)
    var firstUnit;
    do
        firstUnit = ubind(unit);
    while firstUnit == null;

    var x = @thisx;
    do
        flag(x);
        x += 4;
        ubind(unit);
    while @unit != firstUnit;
end;

void lineUpUnits(y, unit)
    var firstUnit = ubind(unit);
    do
        var x = @unit.@flag;
        itemDrop(@air, 1000);
        move(x, y);
        if @unit.@mining then stop(); end;
        ubind(unit);
    while @unit != firstUnit;
end;
