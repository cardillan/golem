#set target = 8;
#set syntax = strict;
#set sort-variables;

module CoreController;

remote noinit var
    core,
    press,
    smelter,
    kiln,
    generator,
    battery,
    factory,
    unloader2,
    generator2,

    pressTarget,
    smelterTarget,
    kilnTarget,
    unitToProduce;

// TODO error when backgroundProcess() contains local variables
var lastProgress = -1;

void backgroundProcess()
    while true do
        press.enabled = core.@graphite < pressTarget;
        smelter.enabled = core.@silicon < smelterTarget;
        kiln.enabled = core.@metaglass < kilnTarget;
        generator.enabled = battery.@totalPower < 1000;
        generator2.enabled = battery.@totalPower < 3500;

        if factory == null then continue; end;

        if unitToProduce == null then
            factory.enabled = false;
            factory.config = @mono;
            lastProgress = -1;
        elsif lastProgress > factory.@progress + 0.1 then
            unitToProduce = null;
            factory.enabled = false;
            factory.config = @mono;
            lastProgress = 0;
        elsif factory.config != unitToProduce then
            factory.enabled = true;
            factory.config = unitToProduce;
            lastProgress = 0;
        else
            factory.enabled = true;
            lastProgress = factory.@progress;
        end;

        if generator2 != null then
            if generator2.@coal < 3 then
                unloader2.config = @coal;
                continue;
            end;
        end;

        if factory.@silicon < 30 then
            unloader2.config = @silicon;
        else
            unloader2.config = @lead;
        end;
    end;
end;
