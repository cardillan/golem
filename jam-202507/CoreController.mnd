#set target = 8;
#set syntax = strict;

module CoreController;

linked
    press1,
    smelter1,
    kiln1,
    generator1,
    battery1,
    factory1,
    unloader2,
    generator2,
    shard1;

remote var
    pressTarget = 0,
    smelterTarget = 30,
    kilnTarget = 30,
    unitToProduce = null;

// TODO error when backgroundProcess() contains local variables
var lastProgress = -1;

void backgroundProcess()
    while true do
        press1.enabled = shard1.@copper < pressTarget;
        smelter1.enabled = shard1.@silicon < smelterTarget;
        kiln1.enabled = shard1.@metaglass < kilnTarget;
        generator1.enabled = battery1.@totalPower < 3800;

        if factory1 == null then continue; end;

        if unitToProduce == null then
            factory1.enabled = false;
            factory1.config = @mono;
            lastProgress = -1;
        elsif lastProgress > factory1.@progress + 0.1 then
            unitToProduce = null;
            factory1.enabled = false;
            factory1.config = @mono;
            lastProgress = 0;
        elsif factory1.config != unitToProduce then
            factory1.enabled = true;
            factory1.config = unitToProduce;
            lastProgress = 0;
        else
            factory1.enabled = true;
            lastProgress = factory1.@progress;
        end;

        if generator2 != null then
            if generator2.@coal < 3 then
                unloader2.config = @coal;
                continue;
            end;
        end;

        if factory1.@silicon < 30 then
            unloader2.config = @silicon;
        else
            unloader2.config = @lead;
        end;
    end;
end;
