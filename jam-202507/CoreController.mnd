#set target = 8;
#set syntax = strict;
#set sort-variables;

// Note: remake this so that status gets updated via remote calls

module CoreController;

noinit remote var
    timer,
    core,
    display,
    bank,
    press,
    unloader1,
    smelter,
    smelter2,
    kiln,
    generator,
    battery,
    factory,
    unloader2,
    generator2,
    multipress,
    payloadRouter,
    reconstructor,
    unloader4,
    multiReconstructor,
    unloader6,

    // Lead and/or silicone will only be sent to the Air Factory
    // if they are above these limits
    leadReserve,
    siliconReserve,
    graphiteReserve,

    factoryUnit,
    targetUnit,
    loopTicks;

remote var
    flares = 0,
    monos = 0,
    polys= 1,
    megas = 0,

    pressTarget = 20,
    smelterTarget = 20,
    kilnTarget = 20,
    flareTarget = 0,
    monoTarget = 0,
    polyTarget = 0,
    megaTarget = 0;

// Due to Mindcode restriction of requiring a single module from two places, we need to declare these
// variables in the display processor explicitly.
remote display var
    displayFlares,
    displayMonos,
    displayPolys,
    displayMegas,
    displayNextUnit,
    displayUnitReset;

external bank[511] var logPosition;

void log(encodedId)
    bank[--logPosition] = encodedId + timer.read("time") * 1000;
end;

void logUnit(unit)
    log(@blockCount + unit.@id);
end;

var factoryDone = 0;

void factoryUnitFinished(unit)
    // Do not announce when the
    if unit == targetUnit then
        logUnit(unit);
        case unit
            when @flare then displayFlares = ++flares;
            when @mono  then displayMonos = ++monos;
            when @poly  then displayPolys = ++polys;
            when @mega  then displayMegas = ++megas;
        end;
        displayUnitReset = true;
        factoryDone = 0;
    else
        case unit
            when @flare, @mono then factoryDone = 1;
        end;
    end;
end;

inline def updateIfSmaller(ref value, in a, in b, in newValue)
    mlogSafe("select", out value, "lessThan", in a, in b, in newValue, in value);
end;

inline def fuzzyUpdate(ref state, in value, in limitLow, in limitHigh)
    updateIfSmaller(ref state, value, limitLow, false);
    updateIfSmaller(ref state, limitHigh, value, true);
end;

void backgroundProcess()
    var lastProgress = 0, factoryProgress = 0;
    var lastRecoProgress = 0, reconstructorProgress = 0;
    var lastMultiProgress = 0, multiReconstructorProgress = 0;
    var enableFactory = true, blockFactory = false;

    while true do
        var ticks = @tick;
        var coreSilicon = core.@silicon;
        var coreGraphite = core.@graphite;
        smelter2.enabled = smelter.enabled = coreSilicon < smelterTarget;
        press.enabled = !multipress and coreGraphite < pressTarget;
        multipress.enabled = coreGraphite < pressTarget;
        kiln.enabled = core.@metaglass < kilnTarget;

        if smelter2 then unloader1.config = @coal; end;

        var power = battery.@totalPower;
        if generator2 then
            generator.enabled = power < 10;
            generator2.enabled = power < 3800;
        else
            generator.enabled = power < 800;
        end;

        factoryProgress = factory.@progress;
        reconstructorProgress = reconstructor.@progress;
        multiReconstructorProgress = multiReconstructor.@progress;

        if flares < flareTarget then
            displayNextUnit = targetUnit = factoryUnit = @flare;
            payloadRouter.config = targetUnit;
            blockFactory = false;
        elsif monos < monoTarget then
            displayNextUnit = targetUnit = factoryUnit = @mono;
            payloadRouter.config = targetUnit;
            blockFactory = false;
        elsif polys < polyTarget then
            factoryUnit = factoryDone + polys < polyTarget ? @mono : null;
            displayNextUnit = targetUnit = @poly;
            reconstructor.enabled = true;
            payloadRouter.config = targetUnit;
            blockFactory = reconstructor.@payloadCount * factoryProgress > reconstructorProgress;
        elsif megas < megaTarget then
            factoryUnit = factoryDone + megas < megaTarget ? @mono : null;
            displayNextUnit = targetUnit = @mega;
            reconstructor.enabled = multiReconstructor or reconstructorProgress < 0.75;
            payloadRouter.config = targetUnit;
            blockFactory = false;
        else
            displayNextUnit = targetUnit = factoryUnit = null;
            blockFactory = false;
        end;

        fuzzyUpdate(ref enableFactory, power, 200, 500);

        if factoryUnit == null then
            factory.enabled = false;
            factory.config = @mono;
            lastProgress = 0;
        elsif lastProgress > factoryProgress + 0.1 then
            factory.enabled = enableFactory and not blockFactory;
            factoryUnitFinished(factoryUnit);
            lastProgress = 0;
        elsif factory.config != factoryUnit then
            factory.enabled = enableFactory and not blockFactory;
            factory.config = factoryUnit;
            lastProgress = 0;
        else
            factory.enabled = enableFactory and not blockFactory;
            lastProgress = factoryProgress;
        end;

        if lastRecoProgress > reconstructorProgress + 0.1 then
            factoryUnitFinished(@poly);
            lastRecoProgress = 0;
        else
            lastRecoProgress = reconstructorProgress;
        end;

        if lastMultiProgress > multiReconstructorProgress + 0.1 then
            factoryUnitFinished(@mega);
            lastMultiProgress = 0;
        else
            lastMultiProgress = multiReconstructorProgress;
        end;

        if generator2.@coal < 5 * generator2 then
            unloader2.config = @coal;
            continue;
        end;

        unloader4.enabled = coreSilicon > siliconReserve and coreGraphite > graphiteReserve;

        var leadLimit = (factoryUnit == @mono) * 30;
        if factory.@lead < leadLimit and core.@lead > leadReserve then
            unloader2.config = @lead;
        elsif coreSilicon > siliconReserve then
            unloader2.config = @silicon;
        else
            unloader2.config = @coal;
        end;

        unloader6.config = multiReconstructor.@silicon >= 130 and multiReconstructor.@titanium < 80 ? @titanium : @silicon;

        loopTicks = @tick - ticks;
    end;
end;
