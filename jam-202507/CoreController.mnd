#set target = 8;
#set syntax = strict;
#set sort-variables;

module CoreController;

remote noinit var
    core,
    display,
    bank,
    press,
    smelter,
    kiln,
    generator,
    battery,
    factory,
    unloader2,
    generator2,

    pressTarget,
    smelterTarget,
    kilnTarget,
    unitToProduce,

    buildUnits,
    monos,
    flares,
    polys;

remote display ".monos" var displayMonos;
remote display ".flares" var displayFlares;

// TODO error when backgroundProcess() contains local variables
var lastProgress = 0, factoryProgress = 0;

// TODO external bank[511] var logPosition; doesn't work
external bank[511 .. 511] var logPosition;

void log(encodedId)
    bank[--logPosition] = encodedId + floor(@second) * 1000;
end;

void logUnit(unit)
    log(@blockCount + unit.@id);
end;

void unitFinished()
    logUnit(unitToProduce);
    if unitToProduce == @flare then
        displayFlares = ++flares;
    else
        displayMonos = ++monos;
    end;

    unitToProduce = monos < 8 ? @mono : flares < 8 ? @flare : null;
end;

void main()
    while true do
        press.enabled = core.@graphite < pressTarget;
        smelter.enabled = core.@silicon < smelterTarget;
        kiln.enabled = core.@metaglass < kilnTarget;
        generator.enabled = battery.@totalPower < 3500;
        generator2.enabled = battery.@totalPower < 3800;

        if factory == null then continue; end;

        if buildUnits then
            factoryProgress = factory.@progress;

            if unitToProduce == null then
                factory.enabled = false;
                factory.config = @mono;
                lastProgress = 0;
            elsif lastProgress > factoryProgress + 0.1 then
                unitFinished();
                lastProgress = 0;
            elsif factory.config != unitToProduce then
                factory.enabled = true;
                factory.config = unitToProduce;
                lastProgress = 0;
            else
                factory.enabled = true;
                lastProgress = factoryProgress;
            end;
        else
            factory.enabled = false;
        end;

        if generator2 != null then
            if generator2.@coal < 3 then
                unloader2.config = @coal;
                continue;
            end;
        end;

        var siliconLimit = factory.config == @mono ? 60 : 30;
        if factory.@silicon < siliconLimit and core.@silicon > 0 then
            unloader2.config = @silicon;
        else
            unloader2.config = @lead;
        end;
    end;
end;

void backgroundProcess()
    main();
end;
